apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.stehno.natives'
apply plugin: 'application'
apply plugin: 'distribution'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "gradle.plugin.com.stehno:gradle-natives:0.3.0"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.esotericsoftware.kryo', name: 'kryo', version: '2.22'
    compile group: 'com.esotericsoftware', name: 'kryonet', version: '2.22.0-RC1'
    compile group: 'org.l33tlabs.twl', name: 'pngdecoder', version: '1.0'
    compile group: 'com.miglayout', name: 'miglayout-swing', version: '4.2'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.9.1'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version: '2.9.1'

    compile fileTree(dir: 'src/main/resources/lib', include: ['*.jar'])
    compile fileTree(dir: 'src/main/resources/lib/launcher', include: ['*.jar'])
}

natives {
    outputDir = 'libs/libs'
}

jar {
    manifest {
        attributes 'Main-Class': 'uk.co.pbellchambers.maceswinger.client.GameClient'
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

mainClassName = 'uk.co.pbellchambers.maceswinger.client.GameClient'

task copyCoreModule(type: Copy) {
    from 'build/classes/main/uk/co/pbellchambers/maceswinger/core'
    into 'build/modules/modules/core'
}

task copyModuleResources(type: Copy) {
    from 'modules'
    into 'build/modules/modules'
}

task copyNativeLibraries(type: Copy) {
    from 'build/libs/libs'
    into 'libs'
}

distributions {
    maceSwinger {
        baseName = 'Mace-Swinger'
        contents {
            from { 'build/libs' }
            from { 'build/modules' }
        }
    }
}

includeNatives.dependsOn 'jar'
copyNativeLibraries.dependsOn 'includeNatives'
copyModuleResources.dependsOn 'copyNativeLibraries'
copyCoreModule.dependsOn 'copyModuleResources'
maceSwingerDistZip.dependsOn 'copyCoreModule'